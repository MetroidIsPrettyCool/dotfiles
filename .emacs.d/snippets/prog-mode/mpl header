# -*- mode: snippet -*-
# name: mpl header
# key: mpl
# condition: yas-not-string-or-comment-condition
# type: command
# --
(let ((outer-buffer (current-buffer)))
  (yas-expand-snippet
   (with-temp-buffer
     ;; TODO: figure out more precisely what we should clone from the parent mode. This is just every variable I could
     ;; find defined that started with `comment-*'
     (dolist (var '(comment-add
                    comment-auto-fill-only-comments
                    comment-column
                    comment-combine-change-calls
                    comment-continue
                    comment-empty-lines
                    comment-end
                    comment-end-can-be-escaped
                    comment-end-skip
                    comment-fill-column
                    comment-indent-function
                    comment-inline-offset
                    comment-insert-comment-function
                    comment-line-break-function
                    comment-multi-line
                    comment-padding
                    comment-quote-nested
                    comment-quote-nested-function
                    comment-region-function
                    comment-start
                    comment-start-skip
                    comment-style
                    comment-styles
                    comment-use-global-state
                    comment-use-syntax
                    comment-use-syntax-ppss))
       (when-let* ((entry (assq var (buffer-local-variables outer-buffer))))
         (set (make-local-variable var) (cdr entry))))

     (insert "This Source Code Form is subject to the terms of the Mozilla Public\n")
     (insert "License, v. 2.0. If a copy of the MPL was not distributed with this\n")
     (insert "file, You can obtain one at <https://mozilla.org/MPL/2.0/>.\n\n")
     (insert "Copyright (c)")
     (insert " $\{1:`(format-time-string \"%Y\")`\}")
     (insert " $\{2:`(or user-full-name \"\")`\}")
     (insert " $\{3:$(if (equal yas-text \"\") \"\" \"<\")\}")
     (insert "$\{3:`(or user-mail-address \"\")`\}")
     (insert "$\{3:$(if (equal yas-text \"\") \"\" \">\")\}")
     (comment-region (point-min) (point-max))
     (insert "$0")
     (buffer-string))))